plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.3.4.BUILD-SNAPSHOT'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "com.google.protobuf" version "0.8.14"
    id 'com.google.cloud.tools.jib' version '2.5.0'
    id 'com.diffplug.spotless' version '5.8.2'
}

group = 'io.treactor'
version = '0.1-SNAPSHOT'
sourceCompatibility = '11'

def otelapi = '1.0.0'
def otelsdk = '1.0.0'
def otelagent = '1.0.0'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

configurations {
    all*.exclude group: 'ch.qos.logback'
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    all*.exclude group: 'org.slf4j', module: 'jul-to-slf4j'
    agent
}

dependencies {
    compile "io.grpc:grpc-protobuf:1.34.1"
    compile "io.grpc:grpc-stub:1.34.1"
    runtime "com.google.protobuf:protobuf-java:3.14.0"
    runtime "com.google.protobuf:protobuf-java-util:3.14.0"
    compile "io.opentelemetry:opentelemetry-api:$otelapi"
    compile "io.opentelemetry:opentelemetry-sdk:$otelsdk"

    runtime "io.opentelemetry:opentelemetry-exporter-otlp:$otelsdk"
    runtime "io.opentelemetry:opentelemetry-exporter-prometheus:1.0.0-alpha"
    compile "io.opentelemetry:opentelemetry-sdk-extension-zpages:1.0.0-alpha"
    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:$otelagent:all"

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-web', version: '2.14.0'

    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.11.1'
//    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-datatype-jsr310', version: '2.11.1'

    implementation "io.grpc:grpc-netty-shaded:1.32.1"

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

task webpack(type: Exec) {
//	inputs.file("package-lock.json")
//	inputs.dir("app")
    commandLine "$projectDir/node_modules/.bin/webpack", "app/index.js", "$buildDir/js/bundle.js"
//	outputs.dir("$buildDir/js")
//	outputs.dir("$projectDir/src/main/resources/static")
}

task webpack2(type: Copy) {
    from "$buildDir/js"
    into "$projectDir/src/main/resources/static/js"
}

task copyAgentJar(type: Copy) {
    from configurations.agent
    into "src/main/jib/app"
    rename { fileName -> "otel-javaagent.jar" }
}

jib {
    to {
        image = 'gcr.io/treactor/treactor-java'
        credHelper = 'osxkeychain'
        tags = ['0.1.3']
    }
    container {
        jvmFlags = ['-javaagent:/app/otel-javaagent.jar',
                    '-Xms512m',
                    '-Xdebug']
        mainClass = 'io.treactor.springboot.Application'
        ports = ['3330']
        format = 'OCI'
    }
}
tasks.jib.dependsOn(copyAgentJar)

spotless {
    java {
        removeUnusedImports()
        googleJavaFormat()
    }
}
